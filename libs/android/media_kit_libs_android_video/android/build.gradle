import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.6/full-arm64-v8a.jar", "md5": "54475d7c87b2dd8e922ed358accfdd58", "destination": file("$buildDir/v1.1.5/default-arm64-v8a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.6/full-armeabi-v7a.jar", "md5": "0b9f69a148fa7515dd65f814c1d0cd7d", "destination": file("$buildDir/v1.1.5/default-armeabi-v7a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.6/full-x86_64.jar", "md5": "00fe4dc03965f1acc7906fda53beb03f", "destination": file("$buildDir/v1.1.5/default-x86_64.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.6/full-x86.jar", "md5": "37c5068af72902f1e804d0f2cd2a9654", "destination": file("$buildDir/v1.1.5/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)
